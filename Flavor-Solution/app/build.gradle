apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    // default config for app
    defaultConfig {
        applicationId "com.eric.default"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }

    // build types for debug and release variant
    buildTypes {
        debug {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Flavors
    productFlavors {
        FlavorA {
            applicationId "com.eric.flavora"
            minSdkVersion 21
            targetSdkVersion 25
            versionCode 1
            versionName "1.0.0"
        }

        FlavorB {
            applicationId "com.eric.flavorb"
            minSdkVersion 21
            targetSdkVersion 25
            versionCode 2
            versionName "2.0.0"
        }

        FlavorC {
            applicationId "com.eric.flavorc"
            minSdkVersion 21
            targetSdkVersion 25
            versionCode 3
            versionName "3.0.0"
        }
    }

    // create different apk name
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                def apkName = generateApkName(variant.flavorName, variant.versionName,
                        variant.versionCode, variant.buildType.getName())
                output.outputFile = new File(output.outputFile.getParent(), "${apkName}")
            }

        }

    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

/**
 * Generate the APK name
 * @param flavor product flavor
 * @param versionName apk's version name
 * @param versionCode apk's version code
 * @param buildType apk's build type, release or debug
 * @return apk file name, i.e FlavorA-v1.0.0-1-debug.apk
 */
def generateApkName(String flavor, String versionName, int versionCode, String buildType) {
    def name = flavor.capitalize()

    // add version name
    name += "-v" + versionName

    // add version code
    name += "-" + versionCode

    // add build type
    name += "-" + buildType

    return name + ".apk"
}

